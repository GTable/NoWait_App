apply plugin: 'com.android.library'

group = 'expo.modules.kakaologin'
version = '0.7.6'

def expoModulesCorePlugin = new File(project(":expo-modules-core").projectDir.absolutePath, "ExpoModulesCorePlugin.gradle")
apply from: expoModulesCorePlugin
applyKotlinExpoModulesCorePlugin()
useCoreDependencies()
useExpoPublishing()

// If you want to use the managed Android SDK versions from expo-modules-core, set this to true.
// The Android SDK versions will be bumped from time to time in SDK releases and may introduce breaking changes in your module code.
// Most of the time, you may like to manage the Android SDK versions yourself.
def useManagedAndroidSdkVersions = false
if (useManagedAndroidSdkVersions) {
  useDefaultAndroidSdkVersions()
} else {
  buildscript {
    // Simple helper that allows the root project to override versions declared by this library.
    ext.safeExtGet = { prop, fallback ->
      rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    }
  }
  project.android {
    compileSdkVersion safeExtGet("compileSdkVersion", 35)
    defaultConfig {
      minSdkVersion safeExtGet("minSdkVersion", 24)
      targetSdkVersion safeExtGet("targetSdkVersion", 35)
    }
  }
}

android {
  namespace "expo.modules.kakaologin"
  defaultConfig {
    versionCode 1
    versionName "0.7.6"

    // 카카오 네이티브 앱 키를 AndroidManifest에 주입
    // 환경변수에서만 읽기 (Expo CLI가 .env.local을 자동으로 export)
    def kakaoNativeAppKey = System.getenv('KAKAO_NATIVE_APP_KEY')
    if (!kakaoNativeAppKey) {
      throw new IllegalStateException("KAKAO_NATIVE_APP_KEY must be set as environment variable")
    }
    manifestPlaceholders = [kakaoNativeAppKey: kakaoNativeAppKey]
  }
  lintOptions {
    abortOnError false
  }
}

dependencies {
  implementation "com.kakao.sdk:v2-user:2.23.1"
}
